*** Settings ***
Library  Collections
Library  Selenium2Library
Library  String

Suite Setup  Start Session  http://localhost:4000/archives
Suite Teardown  Close All Browsers

*** Variables ***
${HREF_LINK} =  //a[string-length(@href) > 0]

*** Test Cases ***
Analyze Page
  Analyze Links

*** Keywords ***
Analyze Links
  @{to_visit} =  Create List
  @{found_404} =  Create List
  @{found_ok} =  Create List
  ${links} =  Get Matching Xpath Count  ${HREF_LINK}
  Log  "Going to check ${links} links"
  : FOR  ${link}  IN RANGE  ${links}
  \  ${destination} =  Get Element Attribute  xpath=(${HREF_LINK})[${link}+1]@href 
  \  ${status} =  Run Keyword And Ignore Error  List Should Not Contain Value  ${to_visit}  ${destination}
  \  Run Keyword If  ${status}[0]=="PASS"  Append To List  ${to_visit}  ${destination}
  ${places} =  Get Length  ${to_visit}
  Log  "Number of entries in to_visit list is ${places}"
  : FOR  ${link}  IN  @{to_visit}
  \  Go To  ${link}
  \  ${status} =  Run Keyword And Ignore Error  Wait Until Page Contains Element  xpath=//meta[@name="author"]
  \  Run Keyword If  ${status}[0]=="FAIL"  Append To List  ${found_404}  ${link}
  \  Run Keyword If  ${status}[0]=="PASS"  Append To List  ${found_ok}  ${link}
  ${items_404} =  Get Length  ${found_404}
  ${items_ok} =  Get Length  ${found_ok}
  Log  "Found 404 links ${items_404} are ${found_404}"
  Log  "Found OK links ${items_ok} are ${found_ok}"
  
Start Session
  [Arguments]  ${url}
  Set Selenium Timeout  60s
  Open Browser  ${url}
